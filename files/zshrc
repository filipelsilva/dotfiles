############# Fzf ############# {{{

# Ubuntu uses fdfind, all other use fd. Standardization.
if (( ${+commands[fdfind]} )); then
	alias fd=fdfind
fi

# Make fzf not collide with zsh
export FZF_COMPLETION_TRIGGER='++'

local color00='#262626'
local color01='#3a3a3a'
local color02='#4e4e4e'
local color03='#8a8a8a'
local color04='#949494'
local color05='#dab997'
local color06='#d5c4a1'
local color07='#ebdbb2'
local color08='#d75f5f'
local color09='#ff8700'
local color0A='#ffaf00'
local color0B='#afaf00'
local color0C='#85ad85'
local color0D='#83adad'
local color0E='#d485ad'
local color0F='#d65d0e'

export FZF_DEFAULT_OPTS=" \
--color=bg+:$color01,bg:$color00,spinner:$color0C,hl:$color0D \
--color=fg:$color04,header:$color0D,info:$color0A,pointer:$color0C \
--color=marker:$color0C,fg+:$color06,prompt:$color0A,hl+:$color0D \
--layout=reverse-list \
--info=inline \
--height=80% \
--multi \
--preview '([[ -f {} ]] && (bat --style=numbers --color=always {} || cat {})) || ([[ -d {} ]] && (tree -C {} | less)) || echo {} 2> /dev/null | head -200' \
--bind '?:toggle-preview' \
"

# Fzf using fd instead of find
local FD_DEFAULT_OPTS="--follow"
if (( ${+commands[fdfind]} )); then
	local FD_NAME="fdfind"
elif (( ${+commands[fd]} )); then
	local FD_NAME="fd"
fi
export FZF_DEFAULT_COMMAND="$FD_NAME $FD_DEFAULT_OPTS"
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
export FZF_ALT_C_COMMAND="$FZF_DEFAULT_COMMAND --type d"

_fzf_compgen_path() {
	fd . "$1"
}

_fzf_compgen_dir() {
	fd --type d . "$1"
}

# }}}

############# Aliases / Keybinds ############# {{{

# Temporary
alias links="bat ~/OneDrive/tecnico/2º\ Ano/2º\ Semestre/Calendários/links.txt"

# Deemix
alias deemix="python3 /home/$USER/Programs/deemix-pyweb/server.py"

# Tor Browser
alias tor-browser="(cd /home/$USER/Programs/tor-browser_en-US && ./start-tor-browser.desktop)"

# Open folders/files as if you were in the file manager
alias open="xdg-open"

# Zsh configuration and reload
alias zshsource="source ~/.zshrc && echo 'sourced zshrc'"
alias zshconfig="nvim ~/.zshrc && zshsource"

# Basic commands
alias ".."="cd .."
alias -- -='cd -'
alias cp="cp -r"
alias mkdir="mkdir -p"
alias ip='ip --color=auto'

# Update packages
alias pipup="pip_upgrade_outdated -3"
alias cargoup="cargo install-update -a"

# See all processes
alias psa="ps aux | fzf ${FZF_DEFAULT_OPTS} --header='[name:process]'"

# Aliases for tmux
alias t="tmux"
alias tp="tmuxp"

# Nvim to vim transition
alias v="nvim"

# Exa as ls
alias ls="exa"
alias lsa="ls -lha"
alias l="exa -lbF --git"
alias ll="exa -lbGF --git"
alias llm="exa -lbGd --git --sort=modified"
alias la="exa -lbhHigUmuSa --time-style=long-iso --git --color-scale"
alias lx="exa -lbhHigUmuSa@ --time-style=long-iso --git --color-scale"
alias lS="exa -1"
alias lt="exa --tree --level=2"

# Ubuntu has bat as batcat
if (( ${+commands[batcat]} )); then
	alias bat=batcat
fi

# }}}

############# Functions ############# {{{

# [F]ind-[I]n-[F]ile
function fif() {
	if [ ! "$#" -gt 0 ]; then
		echo "Need a string to search for!";
		return 1;
	fi
	rg --files-with-matches --no-messages "$1" | fzf $FZF_PREVIEW_WINDOW --header='[find in file]' --preview "rg --ignore-case --pretty --context 10 '$1' {}"
}

# [Li]nk[du]mp
function lidu() {
	lynx -dump -listonly -nonumbers $1 | grep .pdf > dump.txt
	wget -i dump.txt
	rm dump.txt
}

# [K]ill [P]rocess
function kp() {
	local pid=$(ps -ef | sed 1d | eval "fzf -m --header='[kill:process]'" | awk '{print $2}')
	if [ "x$pid" != "x" ]
	then
		echo $pid | xargs kill -${1:-9}
		kp
	fi
}

# [F]ind [P]ath
function fp() {
	local loc=$(echo $PATH | sed -e $'s/:/\\\n/g' | eval "fzf --header='[find:path]'")
	if [[ -d $loc ]]; then
		echo "$(rg --files $loc | rev | cut -d"/" -f1 | rev)" | eval "fzf --header='[find:exe] => ${loc}' >/dev/null"
		fp
	fi
}

# [P]ackage [U]pdate
function pu {
	if (( ${+commands[apt]} )); then
		sudo apt update && sudo apt upgrade
	elif (( ${+commands[pacman]} )); then
		sudo pacman -Syu
		if (( ${+commands[yay]} )); then
			yay -Syu --devel --timeupdate
		fi
	fi
}

# }}}

############# Plugins/Themes/Other ############# {{{

# Command history
HISTFILE=$HOME/.zsh_history
HISTSIZE=50000
SAVEHIST=50000
setopt extended_history
setopt share_history
setopt hist_find_no_dups
setopt hist_ignore_space

# Prompt
autoload -Uz vcs_info
precmd_vcs_info() { vcs_info }
precmd_functions+=( precmd_vcs_info )
setopt prompt_subst
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr '+'
zstyle ':vcs_info:*' unstagedstr '*'
zstyle ':vcs_info:git:*' formats '%F{cyan}:%b%u%c%f'
PROMPT='%B%F{magenta}%n@%m %F{blue}%1~${vcs_info_msg_0_} %(?..%F{red}%? )%F{magenta}$%b%f '

# Exa colors
export EXA_COLORS="lp=33:bl=38;5;220:da=38;5;238:uu=38;5;240:un=38;5;160:\
gu=38;5;240:gn=38;5;160:ur=38;5;245:uw=38;5;245:ux=38;5;245:ue=38;5;245:\
gr=38;5;242:gw=38;5;242:gx=38;5;242:tr=38;5;242:tw=38;5;242:tx=38;5;242"

# Editor
export EDITOR=nvim

# Pip
export PATH=$PATH:/home/$USER/.local/bin

# Go
export PATH=$PATH:/home/$USER/go/bin

# Bat theme
export BAT_THEME="base16"

# Vim-mode
MODE_INDICATOR=""
MODE_CURSOR_VIINS="bar"
MODE_CURSOR_VISUAL="block"
MODE_CURSOR_VLINE="block"

# Zoxide integration
eval "$(zoxide init zsh --cmd j)"

# TheFuck integration
eval $(thefuck --alias)

# Forgit
source /home/$USER/.config/forgit/forgit.plugin.zsh

# Completion menu
eval "$(dircolors)"
zstyle ':completion:*:*:*:*:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion:*' special-dirs true
# zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion::complete:*' use-cache on
zstyle ':completion::complete:*' cache-path "$HOME/.zsh/zcompcache"
autoload -U compinit && compinit
autoload -U +X bashcompinit && bashcompinit

# Plugins
source $HOME/.zsh/zsh-autosuggestions/zsh-autosuggestions.zsh
source $HOME/.zsh/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh
source $HOME/.zsh/zsh-vim-mode/zsh-vim-mode.plugin.zsh

# Fzf
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# }}}
